/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

ext.rotEnabled = false

import java.util.regex.Matcher
import java.util.regex.Pattern
import groovy.io.FileType

apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "info.zamojski.soft.towercollector"
        minSdkVersion 14
        targetSdkVersion 20
        versionCode 2001045
        versionName "2.0.10"

        vectorDrawables.useSupportLibrary = true

        Properties props = getCustomProperties()
        println 'Defining custom build config fields'
        assert props
        // ACRA
        buildConfigField "Boolean", "ACRA_SEND_REPORTS_IN_DEV_MODE", props['acra_sendReportsInDevMode']
        buildConfigField "Boolean", "ACRA_SETTINGS_AVAILABLE", props['acra_settings_available']
        buildConfigField "String", "ACRA_FORM_URI", props['acra_formUri']
        buildConfigField "String", "ACRA_HTTP_METHOD", props['acra_httpMethod']
        buildConfigField "String", "ACRA_REPORT_TYPE", props['acra_reportType']
        buildConfigField "String", "ACRA_FORM_URI_BASIC_AUTH_LOGIN", rot135(props['acra_formUriBasicAuthLogin'])
        buildConfigField "String", "ACRA_FORM_URI_BASIC_AUTH_PASSWORD", rotHex(props['acra_formUriBasicAuthPassword'])
        // Analytics
        buildConfigField "Boolean", "ANALYTICS_AVAILABLE", props['analytics_available']
        // MLS
        buildConfigField "String", "MLS_API_KEY", rotHex(props['mls_apiKey'])
        // Other
        buildConfigField "String", "UPDATE_CHECK_FEED_URI", props['other_updateCheckFeedUri']
        buildConfigField "String", "CONTACT_EMAIL", rot135(props['other_contactEmail'])
        buildConfigField "Long", "BUILD_DATE_TIME", String.format("%dL", new Date().getTime())
    }
    signingConfigs {
        release {
            keyAlias ''
            storeFile null
            storePassword ''
            keyPassword ''
        }
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
            shrinkResources true
            debuggable false
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions "environment", "market"
    productFlavors {
        develop {
            versionNameSuffix '-dev'
            dimension "environment"
        }
        production {
            dimension "environment"
        }
        official {
            dimension "market"
            buildConfigField "String", "MARKET_NAME", '"Official"'
        }
        fdroid {
            dimension "market"
            buildConfigField "String", "MARKET_NAME", '"F-Droid"'
        }
    }
    lintOptions{
        disable 'MissingTranslation', 'ExpiredTargetSdkVersion'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    officialImplementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'ch.acra:acra-http:5.4.0'
    implementation 'ch.acra:acra-notification:5.4.0'
    implementation 'org.sufficientlysecure:html-textview:3.6'
    implementation 'com.squareup.okhttp3:okhttp:3.12.4' // TODO: newer requires min SDK 21
    implementation 'com.github.hotchemi:permissionsdispatcher:3.3.1' // TODO: newer removes native Fragment support
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:3.3.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'
}

allprojects {
    afterEvaluate { project ->
        def configName = 'release'

        def props = getCustomProperties()
        assert props
        // Signing
        if (android.signingConfigs.hasProperty(configName)) {
            println 'Defining custom signing configuration'
            def signFile = file(props['sign_storeFile'])
            assert signFile.exists()
            android.signingConfigs[configName].storeFile = signFile
            android.signingConfigs[configName].storePassword = props['sign_storePassword']
            android.signingConfigs[configName].keyAlias = props['sign_keyAlias']
            android.signingConfigs[configName].keyPassword = props['sign_keyPassword']
        }
    }
}

Properties getCustomProperties() {
    def currentFlavor = getCurrentFlavor(2)
    // Existing file is required to generate flavors
    def propsFile = rootProject.file("app/properties/example.properties")
    if (currentFlavor) {
        propsFile = rootProject.file("app/properties/private-${currentFlavor}.properties")
        if (!propsFile.exists()) {
            propsFile = rootProject.file("app/properties/private.properties")
        }
    }
    if (propsFile.exists()) {
        println "Loading private custom properties configuration from: $propsFile"
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        rotEnabled = props['other_decode']
        //println props
        return props
    }
    return null
}

String getCurrentFlavor(int dimensionIndex) {
    def gradle = getGradle()
    def taskRequest = gradle.getStartParameter().getTaskRequests().toString()

    def taskName = taskRequest.contains('assemble') ? 'assemble' : 'generate'
    def regex = "$taskName([A-Z]\\w*)([A-Z]\\w*)(Release|Debug)"
    Pattern pattern = Pattern.compile(regex)

    Matcher matcher = pattern.matcher(taskRequest)
    if (matcher.find())
        return matcher.group(dimensionIndex).toLowerCase()
    println "Gradle flavor name not found"
    return null;
}

String rot135(String s) {
    if (!rotEnabled) {
        println 'Rot disabled'
        return s
    }
    StringBuilder sb = new StringBuilder()
    for (int i = 0; i < s.length(); i++) {
        int num = s[i]
        if ((num >= 97 && num <= 109) || (num >= 65 && num <= 77)) {
            num += 13
        } else if ((num >= 110 && num <= 122) || (num >= 78 && num <= 90)) {
            num -= 13
        } else if (num >= 48 && num <= 52) {
            num += 5
        } else if (num >= 53 && num <= 57) {
            num -= 5
        }
        sb.append((char) num)
    }
    return sb.toString()
}

String rotHex(String s) {
    if (!rotEnabled) {
        println 'Rot disabled'
        return s
    }
    StringBuilder sb = new StringBuilder()
    for (int i = 0; i < s.length(); i++) {
        int num = s[i]
        if ((num >= 97 && num <= 99) || (num >= 65 && num <= 67)) {
            num += 3
        } else if ((num >= 100 && num <= 102) || (num >= 68 && num <= 70)) {
            num -= 3
        } else if (num >= 48 && num <= 52) {
            num += 5
        } else if (num >= 53 && num <= 57) {
            num -= 5
        }
        sb.append((char) num)
    }
    return sb.toString()
}

// Custom tasks
List<String> getSourceFiles() {
    def files = []
    getProjectDir().eachFileRecurse(FileType.FILES) { file ->
        if (file.name.endsWith('.java')) {
            files << file
        }
    }
    return files
}

def commentLoggingOne(path) {
    def content = new File(path).getText('UTF-8').replaceAll(/(?m)^(\s*)(Log\.[vd].+)/, '$1;//$2')
    new File(path).write(content, 'UTF-8')
}

def uncommentLoggingOne(path) {
    def content = new File(path).getText('UTF-8').replaceAll(/(?m)^(\s*);\/\/\s?(Log\.[vd].+)/, '$1$2')
    new File(path).write(content, 'UTF-8')
}

task commentLoggingAll {
    doLast {
        description = 'Comments all verbose and debug calls to Log class'
        getSourceFiles().each {
            commentLoggingOne(it.path)
        }
    }
}

task uncommentLoggingAll {
    doLast {
        description = 'Uncomments all verbose and debug calls to Log class'
        getSourceFiles().each {
            uncommentLoggingOne(it.path)
        }
    }
}

if (getCurrentFlavor(2) == 'official') {
    println 'Applying Google Services plugin'
    apply plugin: 'com.google.gms.google-services'
}
